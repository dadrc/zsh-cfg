#compdef tmsu

# Zsh completion script for tmsu. Copy this file to
# your Zsh function path, e.g. /usr/share/zsh/site-functions/_tmsu.

local context curcontext="$curcontext" state state_descr line
typeset -A opt_args

_tmsu() {
	local cmd ret=1

	_arguments -C \
		'1: :_tmsu_commands' \
		'*::arg:->args' \
		&& ret=0

	case $state in
		(args)
			cmd="$words[1]"
			if (( $+functions[_tmsu_cmd_${cmd}] ))
			then
				_tmsu_cmd_$cmd
			fi
		;;
	esac
}

_tmsu_commands() {
	typeset -a command_list
	local line

	_call_program tmsu tmsu help --list | \
	while read -A line
	do
		command_list+=($line[1])
	done

	_describe -t commands 'command' command_list "$@"
}

_tmsu_tags() {
	typeset -a tag_list
	local tag

	_call_program tmsu tmsu tags --all | while read -A tag
	do
		tag_list+=$tag[1]:gs/:/\\:/
	done

	_describe -t tags 'tags' tag_list
}

_tmsu_cmd_copy() {
    _arguments -s -w '1:tag:_tmsu_tags' && ret=0
}

_tmsu_cmd_delete() {
	_arguments -s -w '*:tag:_tmsu_tags' && ret=0
}

_tmsu_cmd_dupes() {
	_arguments -s -w '*:file:_files' && ret=0
}

_tmsu_cmd_export() {
	# no arguments
}

_tmsu_cmd_files() {
	_arguments -s -w '*:tag:_tmsu_tags' && ret=0
}

_tmsu_cmd_help() {
	_arguments -s -w '1:command:_tmsu_commands' && ret=0
}

_tmsu_cmd_merge() {
	_arguments -s -w '*:tag:_tmsu_tags' && ret=0
}

_tmsu_cmd_mount() {
	_arguments -s -w '1:file:_files' \
	                 '2:mountpoint:_dirs' \
	&& ret=0
}

_tmsu_cmd_rename() {
	_arguments -s -w '1:tag:_tmsu_tags' && ret=0
}

_tmsu_cmd_repair() {
	_arguments -s -w '*:file:_files' && ret=0
}

_tmsu_cmd_stats() {
    # no arguments
}

_tmsu_cmd_status() {
	_arguments -s -w '*:file:_files' && ret=0
}

_tmsu_cmd_tag() {
	_arguments -s -w '--tags' \
	                 '*:: :->items' \
	&& ret=0

	case $state in
		(items)
			if (( ${+opt_args[--tags]} ))
			then
				if (( CURRENT == 1 ))
				then
					local expl
					_wanted tags expl 'tags' _tmsu_tags
				else
					local expl
					_wanted files expl 'files' _files
				fi
			else
				if (( CURRENT == 1 ))
				then
					local expl
					_wanted files expl 'files' _files
				else
					local expl
					_wanted tags expl 'tags' _tmsu_tags
				fi
			fi
		;;
	esac
}

_tmsu_cmd_tags() {
	_arguments -s -w '*:file:_files' && ret=0
}

_tmsu_cmd_unmount() {
	_arguments -s -w '--all' \
	                 '1:mountpoint:_files' \
	&& ret=0
}

_tmsu_cmd_untag() {
	_arguments -s -w '--all' \
	                 '--tags' \
	                 '*:: :->items' \
	&& ret=0

	case $state in
		(items)
			if (( ${+opt_args[--tags]} ))
			then
				if (( CURRENT == 1 ))
				then
					local expl
					_wanted tags expl 'tags' _tmsu_tags
				else
					local expl
					_wanted files expl 'files' _files
				fi
			elif (( ${+opt_args[--all]} ))
			then
				local expl
				_wanted files expl 'files' _files
			else
				if (( CURRENT == 1 ))
				then
					local expl
					_wanted files expl 'files' _files
				else
					local expl
					_wanted tags expl 'tags' _tmsu_tags
				fi
			fi
		;;
	esac
}

_tmsu_cmd_version() {
	# no arguments
}

_tmsu "$@"
